*** Settings ***
Library           ../lib/Selenium2Improved.py
Resource          ../Common_Resource.txt

*** Keywords ***
Add Release
    [Arguments]    ${name}    ${description}
    ${add_release_button}    Set Variable    //button[text()="Add Release"]
    Wait Until Element Is Visible    ${add_release_button}    30s
    Click Button    ${add_release_button}
    Input Text    //label[text()="*Name:"]/..//input    ${name}
    Click Element    //label[text()="*Start Date:"]/..//input
    Click Element    //div[text()="Today"]
    Click Element    //label[text()="*End Date:"]/..//input
    Click Button    //button[@class="react-datepicker__navigation react-datepicker__navigation--next react-datepicker__navigation--next--with-today-button"]
    Click Element    //div[text()="28"]
    Input Text    //label[text()="*Description:"]/..//textarea    ${description}
    Click Button    //button[text()="Submit"]

Edit Release
    [Arguments]    ${original_name}    ${new_name}    ${description}
    Select Data In Table    ${original_name}
    Click Button    //button[text()="Edit Release"]
    Input Text    //label[text()="*Name:"]/..//input    ${new_name}
    Input Text    //label[text()="*Description:"]/..//textarea    ${description}
    Click Button    //button[text()="Submit"]

Delete Release
    [Arguments]    ${name}
    Select Data In Table    ${name}
    ${delete_release_button}    Set Variable    //button[text()="Delete Release"]
    Wait Until Element Is Visible    ${delete_release_button}    30s
    Click Button    ${delete_release_button}
    Click Button    //button[text()="Submit"]

Schedule Backlog Item
    [Arguments]    ${backlog_item_description}    ${release_name}
    Select Data In Table    ${release_name}
    ${schedule_backlog_item_button}    Set Variable    //button[text()="Schedule Backlog Item"]
    Wait Until Element Is Visible    ${schedule_backlog_item_button}    30s
    Click Button    ${schedule_backlog_item_button}
    Select Data In Table    ${backlog_item_description}
    Click Button    //button[text()="Submit"]
    Wait Until Keyword Succeeds    30s    5s    Page Should Contain Element    //td[text()="${backlog_item_description}"]

Unschedule Backlog Item
    [Arguments]    ${backlog_item_description}    ${release_name}
    Select Data In Table    ${release_name}
    Select Data In Table    ${backlog_item_description}
    Click Button    //button[text()="Unschedule Backlog Item"]
    Click Button    //button[text()="Submit"]
