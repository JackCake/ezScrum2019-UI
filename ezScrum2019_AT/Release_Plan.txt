*** Settings ***
Library           keywords/lib/Selenium2Improved.py
Resource          keywords/Common_Resource.txt
Resource          keywords/Release_Plan/Release_Plan_Keywords.txt
Resource          keywords/Release_Plan/Release_Plan_Setup.txt

*** Variables ***
${backlog_item_description1}    As a user, I want to add the backlog item in the product backlog.
${backlog_item_description2}    As a user, I want to commit the backlog item in the sprint backlog.
${release_name}    Release ezKanban

*** Test Cases ***
Add Release Test
    [Setup]    Add Release Setup
    Select Page    Release Plan
    ${description}    Set Variable    ezKanban Principles
    Add Release    ${release_name}    ${description}
    Wait Until Keyword Succeeds    30s    3s    Page Should Contain Element    //td[text()="${release_name}"]
    Wait Until Keyword Succeeds    30s    3s    Page Should Contain Element    //td[text()="${description}"]
    [Teardown]    Exit ezScrum2019

Edit Release Test
    [Setup]    Edit Release Setup
    Select Page    Release Plan
    ${new_release_name}    Set Variable    Release ezScrum 2019
    ${description}    Set Variable    Product Backlog, Release Plan, Sprint Plan, Sprint Backlog and Retrospective
    Edit Release    ${release_name}    ${new_release_name}    ${description}
    Wait Until Keyword Succeeds    30s    3s    Page Should Contain Element    //td[text()="${new_release_name}"]
    Wait Until Keyword Succeeds    30s    3s    Page Should Contain Element    //td[text()="${description}"]
    [Teardown]    Exit ezScrum2019

Delete Release Test
    [Setup]    Delete Release Setup
    Select Page    Release Plan
    Delete Release    ${release_name}
    Wait Until Keyword Succeeds    30s    3s    Page Should Not Contain Element    //td[text()="${release_name}"]
    [Teardown]    Exit ezScrum2019

Schedule Backlog Item Test
    [Setup]    Schedule Backlog Item Setup
    Select Page    Release Plan
    Schedule Backlog Item    ${backlog_item_description2}    ${release_name}
    Wait Until Keyword Succeeds    30s    3s    Page Should Contain Element    //td[text()="${backlog_item_description2}"]
    [Teardown]    Exit ezScrum2019

Unschedule Backlog Item Test
    [Setup]    Unschedule Backlog Item Setup
    Select Page    Release Plan
    Unschedule Backlog Item    ${backlog_item_description2}    ${release_name}
    Wait Until Keyword Succeeds    30s    5s    Page Should Not Contain Element    //td[text()="${backlog_item_description2}"]
    Select Page    Product Backlog
    Wait Until Keyword Succeeds    30s    3s    Page Should Contain Element    //td[text()="${backlog_item_description2}"]
    [Teardown]    Exit ezScrum2019

Add Backlog Item Test
    [Setup]    Add Backlog Item Setup
    Select Page    Release Plan
    Select Data In Table    ${release_name}
    ${estimate}    Set Variable    3
    ${importance}    Set Variable    100
    Add Backlog Item    ${backlog_item_description1}    ${estimate}    ${importance}
    Wait Until Keyword Succeeds    30s    3s    Page Should Contain Element    //td[text()="${backlog_item_description1}"]
    [Teardown]    Exit ezScrum2019

Edit Backlog Item Test
    [Setup]    Edit Backlog Item Setup
    Select Page    Release Plan
    Select Data In Table    ${release_name}
    ${new_backlog_item_description1}    Set Variable    As a user, I want to edit the backlog item in the product backlog.
    ${estimate}    Set Variable    2
    ${importance}    Set Variable    80
    Edit Backlog Item    ${backlog_item_description1}    ${new_backlog_item_description1}    ${estimate}    ${importance}
    Wait Until Keyword Succeeds    30s    3s    Page Should Contain Element    //td[text()="${new_backlog_item_description1}"]
    [Teardown]    Exit ezScrum2019

Delete Backlog Item Test
    [Setup]    Delete Backlog Item Setup
    Select Page    Release Plan
    Select Data In Table    ${release_name}
    Delete Backlog Item    ${backlog_item_description1}
    Wait Until Keyword Succeeds    30s    3s    Page Should Not Contain Element    //td[text()="${backlog_item_description1}"]
    Select Page    Product Backlog
    Wait Until Keyword Succeeds    30s    3s    Page Should Not Contain Element    //td[text()="${backlog_item_description1}"]
    [Teardown]    Exit ezScrum2019
